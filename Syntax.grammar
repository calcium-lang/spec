# Syntax

CompilationUnit:
    [PackageDeclaration] [ImportDeclarations] TopLevelTypeDeclaration

--

PackageDeclaration:
    "package" PackageName [";"]

ImportDeclarations:
    ImportDeclaration {ImportDeclaration}

TopLevelTypeDeclaration:
    [TopLevelEncapsulation] TypeDeclaration

--

PackageName:
    Identifier {"." Identifier}

ImportDeclaration:
    ExplicitImport
    ImportOnDemand

TopLevelEncapsulation:
    "public"

TypeDeclaration:
    TypedefDeclaration ";"
    EnumDeclaration
    UnionDeclaration
    StructDeclaration
    InterfaceDeclaration

--

ExplicitImport:
    "import" ImportNames ["from" PackageOrTypeName] [";"]

ImportOnDemand:
    "import" "*" "from" PackageOrTypeName [";"]

TypedefDeclaration:
    "typedef" Identifier BaseType

EnumDeclaration:
    "enum" Identifier [BaseType] EnumBody

UnionDeclaration:
    [UnionRawness] "union" Identifier UnionBody

StructDeclaration:
    [StructExtensibility] [StructLayout] "struct" Identifier [BaseTypes] StructBody

InterfaceDeclaration:
    "interface" Identifier [BaseInterfaces] InterfaceBody

--

ImportNames:
    ImportName {"," ImportName}

PackageOrTypeName:
    Identifier {"." Identifier}

BaseType:
    ":" Type

EnumBody:
    "{" EnumConstants [";" BodyDeclarations] "}"

UnionRawness:
    "unsafe" "raw"

UnionBody:
    "{" UnionTypes [";" BodyDeclarations] "}"

StructExtensibility:
    (one of)
    "open" "abstract"

StructLayout:
    "raw"

BaseTypes:
    ":" Type ["," TypeNames]

StructBody:
    "{" [StructBodyDeclarations] "}"

BaseInterfaces:
    ":" TypeNames

InterfaceBody:
    "{" [InterfaceBodyDeclarations] "}"

--

ImportName:
    Identifier ["as" Identifier]

Type:
    PrimitiveType [PointerOrArraySuffix]
    TypeName [PointerOrArraySuffix]
    VoidPointer [PointerOrArraySuffix]
    FunctionType
    "(" FunctionType ")" [TypeAtomicity] [PointerOrArraySuffix]

EnumConstants:
    EnumConstant {"," EnumConstant}

BodyDeclarations:
    BodyDeclaration {BodyDeclaration}

UnionTypes:
    UnionType {"," UnionType}

TypeNames:
    TypeName {"," TypeName}

StructBodyDeclarations:
    StructBodyDeclaration {StructBodyDeclaration}

InterfaceBodyDeclarations:
    InterfaceBodyDeclaration {InterfaceBodyDeclaration}

--

PrimitiveType:
    NumericType [TypeAtomicity]
    "bool" [TypeAtomicity]
    "_char" [TypeAtomicity]

PointerOrArraySuffix:
    PointerSuffix [PointerOrArraySuffix]
    ArrayDim [PointerOrArraySuffix]

TypeName:
    Identifier {"." Identifier}

VoidPointer:
    "unsafe" "void" [ValueMutability] [ValueVolatility] "&" [TypeAtomicity]

FunctionType:
    "(" [ParameterTypes] ")" "->" Result

TypeAtomicity:
    "atomic"

EnumConstant:
    Identifier ["=" ConstantExpression]
    Identifier "=" Block

BodyDeclaration:
    StaticInitializer
    [NestedEncapsulation] MemberDeclaration
    [NestedEncapsulation] TypeDeclaration

UnionType:
    TypedefDeclaration
    EnumDeclaration
    UnionDeclaration
    StructDeclaration

StructBodyDeclaration:
    StaticInitializer
    [StructNestedEncapsulation] StructMemberDeclaration
    [StructNestedEncapsulation] TypeDeclaration

InterfaceBodyDeclaration:
    StaticInitializer
    [InterfaceNestedEncapsulation] InterfaceMemberDeclaration
    [NestedEncapsulation] TypeDeclaration

--

NumericType:
    IntegralType
    FloatingPointType

PointerSuffix:
    [ValueMutability] [ValueVolatility] "&" [ReferenceAliasability] [PointerSize] [TypeAtomicity]

ArrayDim:
    "[" [NumberLiteral] "]" [ArrayRawness] [ArrayLayout]
    "[" Identifier "]" [ArrayRawness] [ArrayLayout]

ValueMutability:
    "var"

ValueVolatility:
    "volatile"

ParameterTypes:
    ThisParameter ["," FixedParameterTypes] ["," VariableArityParameterType]
    FixedParameterTypes ["," VariableArityParameterType]
    VariableArityParameterType

Result:
    "noreturn"
    "void"
    Type

Block:
    "{" [BlockStatements] "}"

StaticInitializer:
    MemberStaticity [StringLiteral] Block

NestedEncapsulation:
    (one of)
    "public" "private"

MemberDeclaration:
    FieldDeclaration
    MethodDeclaration

StructNestedEncapsulation:
    (one of)
    "public" "protected" "private"

StructMemberDeclaration:
    [MemberStaticity] FieldDeclaration
    [MemberStaticity] MethodDeclaration
    ConstructorDeclaration
    DestructorDeclaration

InterfaceNestedEncapsulation:
    "private"

InterfaceMemberDeclaration:
    FieldDeclaration
    [MemberStaticity] MethodDeclaration

--

IntegralType:
    (one of)
    "_ubyte" "_byte" "_ushort" "_short" "_uint" "_int" "_ulong" "_long"

FloatingPointType:
    (one of)
    "_float" "_double"

ReferenceAliasability:
    "aliasable"

PointerSize:
    "wide"

ArrayRawness:
    "unsafe" "raw"

ArrayLayout:
    "packed"

ThisParameter:
    "this" [":" ValueMutability [ValueVolatility] ["&" [ReferenceAliasability] [PointerSize]]]
    "this" ":" ValueVolatility ["&" [ReferenceAliasability] [PointerSize]]
    "this" ":" "&" [ReferenceAliasability] [PointerSize]

FixedParameterTypes:
    FixedParameterType {"," FixedParameterType}

VariableArityParameterType:
    "..." ":" Type

BlockStatements:
    BlockStatement {BlockStatement}

MemberStaticity:
    "static"

FieldDeclaration:
    FieldMutability [ValueVolatility] Identifier [StringLiteral] ":" Type ["=" ConstantExpression] ";"

MethodDeclaration:
    [MethodExtensibility] [MethodOverride] "func" MethodHeader MethodBody

ConstructorDeclaration:
    ConstructorHeader Block

DestructorDeclaration:
    DestructorHeader Block

--

FixedParameterType:
    ":" Type

FieldMutability:
    (one of)
    "var" "const"

MethodExtensibility:
    (one of)
    "open" "abstract"

MethodOverride:
    "override"

MethodHeader:
    Identifier [StringLiteral] "(" [Parameters] ")" "->" Result

MethodBody:
    Block
    ";"

ConstructorHeader:
    "init" [StringLiteral] "(" [Parameters] ")" ["->" Result]

DestructorHeader:
    "deinit" [StringLiteral] "(" [Parameters] ")" ["->" Result]

--

Parameters:
    ThisParameter ["," FixedParameters] ["," VariableArityParameter]
    FixedParameters ["," VariableArityParameter]
    VariableArityParameter

--

FixedParameters:
    FixedParameter {"," FixedParameter}

VariableArityParameter:
    "..." Identifier ":" Type

--

FixedParameter:
    Identifier ":" Type
