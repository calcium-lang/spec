# Syntax
## Packages
CompilationUnit:
    [PackageDeclaration] [ImportDeclarations] TopLevelTypeDeclaration

---

PackageDeclaration:
    "package" PackageName [";"]

ImportDeclarations:
    ImportDeclaration {ImportDeclaration}

TopLevelTypeDeclaration:
    [DeclarationEncapsulation] TypeDeclaration

---

ImportDeclaration:
    ExplicitTypeImportDeclaration
    TypeImportOnDemandDeclaration

DeclarationEncapsulation:
    (one of)
    "public" "protected" "private"

TypeDeclaration:
    TypedefDeclaration [";"]
    EnumDeclaration
    UnionDeclaration
    StructDeclaration

> TypedefBody and the semicolon after TypedefDeclaration are mutually exclusive, and exactly one of them must always be found in rule 1.

---

ExplicitTypeImportDeclaration:
    "import" ImportNames [FromName] [";"]

TypeImportOnDemandDeclaration:
    "import" "*" FromName [";"]

---

ImportNames:
    ImportName {"," ImportName}

FromName:
    "from" PackageOrTypeName

## Names
PackageName:
    Identifier [Version] {"." Identifier [Version]}

ImportName:
    Identifier [Version] ["as" Identifier]

PackageOrTypeName:
    Identifier [Version] {"." Identifier [Version]}

## Typedefs, Enums, Unions and Structs
TypedefDeclaration:
    "typedef" Identifier [Version] BaseType [TypedefBody]

EnumDeclaration:
    [EnumLayout] "enum" Identifier [Version] [BaseType] EnumBody

UnionDeclaration:
    "union" Identifier [Version] UnionBody

StructDeclaration:
    [DeclarationExtensibility] [StructLayout] "struct" Identifier [Version] [BaseType] StructBody

---

Version:
    "@" IntegerLiteral "." IntegerLiteral

BaseType:
    ":" Type

TypedefBody:
    "{" BodyDeclarations "}"

EnumLayout:
    "strict"

EnumBody:
    "{" EnumConstants [";" BodyDeclarations] "}"

UnionBody:
    "{" UnionTypes [";" BodyDeclarations] "}"

DeclarationExtensibility:
    (one of)
    "open" "abstract"

StructLayout:
    (one of)
    "strict" "ordered" "packed"

StructBody:
    "{" [BodyDeclarations] "}"

---

BodyDeclarations:
    BodyDeclaration {BodyDeclaration}

EnumConstants:
    EnumConstant {"," EnumConstant}

UnionTypes:
    UnionType {"," UnionType}

---

BodyDeclaration:
    StaticInitializer
    [DeclarationEncapsulation] MemberDeclaration
    [DeclarationEncapsulation] TypeDeclaration

EnumConstant:
    Identifier ["=" VariableInitializer]

UnionType:
    TypedefDeclaration
    EnumDeclaration
    UnionDeclaration
    StructDeclaration

---

StaticInitializer:
    [NameStrictness] "static" [Version] [StringIdentifier] Block

MemberDeclaration:
    [MemberStaticity] FieldDeclaration
    [MemberStaticity] MethodDeclaration

VariableInitializer:
    Expression
    ArrayInitializer
    StructInitializer

---

NameStrictness:
    "strict"

MemberStaticity:
    "static"

FieldDeclaration:
    ValueMutability [ValueVolatility] [NameStrictness] Identifier [StringIdentifier] ":" Type ["=" VariableInitializer] ";"

MethodDeclaration:
    [DeclarationExtensibility] [MethodOverride] [FunctionStrictness] [FunctionPurity] "func" MethodHeader MethodBody

---

MethodOverride:
    "override"

MethodHeader:
    MethodDeclarator ["->" Result]

MethodBody:
    Block
    ";"

---

MethodDeclarator:
    [NameStrictness] Identifier [Version] [StringIdentifier] [":" TypeName] "(" [Parameters] ")"

---

Parameters:
    ThisParameter ["," FixedParameters] ["," VariableArityParameter]
    FixedParameters ["," VariableArityParameter]
    VariableArityParameter

---

FixedParameters:
    FixedParameter {"," FixedParameter}

VariableArityParameter:
    "..." Identifier [":" Type]

---

FixedParameter:
    Identifier [":" Type]

## Types
Type:
    PrimitiveType [PointerOrArraySuffix]
    TypeName [PointerOrArraySuffix]
    VoidPointerType [PointerOrArraySuffix]
    FunctionType
    "(" FunctionType ")" PointerOrArraySuffix

---

PrimitiveType:
    [TypeAtomicity] NumericType
    [TypeAtomicity] "bool"
    [TypeAtomicity] "_char"

PointerOrArraySuffix:
    PointerSuffix [PointerOrArraySuffix]
    ArrayDim [PointerOrArraySuffix]

TypeName:
    [TypeStrictness] Identifier [Version] {"." Identifier [Version]}
    TypeBareness Identifier [Version] {"." Identifier [Version]}

VoidPointerType:
    "unsafe" "void" "&" [TypeAtomicity]

FunctionType:
    [TypeAtomicity] [FunctionStrictness] [FunctionPurity] "(" [ParameterTypes] ")" "->" Result

---

TypeAtomicity:
    "atomic"

NumericType:
    IntegralType
    FloatingPointType

PointerSuffix:
    [ValueMutability] [ValueVolatility] "&" [PointerWidth] [ReferenceAliasability]
    [ValueMutability] [ValueVolatility] "&" TypeAtomicity [ReferenceAliasability]

ArrayDim:
    "[" [TypeStrictness] [Expression] "]"
    "[" TypeBareness "]"

TypeStrictness:
    "strict"

TypeBareness:
    "unsafe" "bare"

FunctionStrictness:
    "strict"

FunctionPurity:
    ["local"] "const"
    ["local"] "pure"

ParameterTypes:
    ThisParameter ["," FixedParameterTypes] ["," VariableArityParameterType]
    FixedParameterTypes ["," VariableArityParameterType]
    VariableArityParameterType

Result:
    "noreturn"
    "void"
    Type

---

IntegralType:
    (one of)
    "_ubyte" "_byte" "_ushort" "_short" "_uint" "_int" "_ulong" "_long"

FloatingPointType:
    (one of)
    "_float" "_double"

ValueMutability:
    ["unused"] "var"
    ["local"] "const"

ValueVolatility:
    ["local"] "volatile"

PointerWidth:
    [["unsafe"] "unused"] [TypeStrictness] "wide"
    "unsafe" "bare"

ReferenceAliasability:
    ["local"] "aliasable"

ThisParameter:
    "this" [":" TypeName [ValueMutability] [ValueVolatility] ["&" [PointerWidth] [ReferenceAliasability]]]
    "this" ":" ValueMutability [ValueVolatility] ["&" [PointerWidth] [ReferenceAliasability]]
    "this" ":" ValueVolatility ["&" [PointerWidth] [ReferenceAliasability]]
    "this" ":" "&" [PointerWidth] [ReferenceAliasability]

FixedParameterTypes:
    FixedParameterType {"," FixedParameterType}

VariableArityParameterType:
    "..." ":" Type

---

FixedParameterType:
    ":" Type

## Blocks and Statements
Block:
    "{" [BlockStatements] "}"

---

BlockStatements:
    BlockStatement {BlockStatement}

## Expressions

## Array and Struct Initializers
ArrayInitializer:
    "[" [VariableInitializers] "]"

StructInitializer:
    "{" [FieldInitializers] "}"

---

VariableInitializers:
    VariableInitializer {"," VariableInitializer}

FieldInitializers:
    FieldInitializer {"," FieldInitializer}

---

FieldInitializer:
    Identifier "=" VariableInitializer
