# Syntax
## Packages
CompilationUnit:
    [PackageDeclaration] [ImportDeclarations] TopLevelTypeDeclaration

--

PackageDeclaration:
    "package" PackageName [";"]

ImportDeclarations:
    ImportDeclaration {ImportDeclaration}

TopLevelTypeDeclaration:
    [DeclarationEncapsulation] TypeDeclaration

--

ImportDeclaration:
    ExplicitTypeImportDeclaration
    TypeImportOnDemandDeclaration

DeclarationEncapsulation:
    (one of)
    "public" "protected" "private"

TypeDeclaration:
    TypedefDeclaration [";"]
    EnumDeclaration
    UnionDeclaration
    StructDeclaration

> TypedefBody and the semicolon after TypedefDeclaration are mutually exclusive, and exactly one of them must always be found in rule 1.

--

ExplicitTypeImportDeclaration:
    "import" ImportNames [FromName] [";"]

TypeImportOnDemandDeclaration:
    "import" "*" FromName [";"]

--

ImportNames:
    ImportName {"," ImportName}

FromName:
    "from" PackageOrTypeName

## Names
PackageName:
    Identifier {"." Identifier}

ImportName:
    Identifier ["as" Identifier]

PackageOrTypeName:
    Identifier {"." Identifier}

## Typedefs, Enums, Unions and Structs
TypedefDeclaration:
    "typedef" Identifier BaseType [TypedefBody]

EnumDeclaration:
    "enum" Identifier [BaseType] EnumBody

UnionDeclaration:
    "union" Identifier UnionBody

StructDeclaration:
    [DeclarationExtensibility] [StructLayout] "struct" Identifier [BaseType] StructBody

--

BaseType:
    ":" Type

TypedefBody:
    "{" BodyDeclarations "}"

EnumBody:
    "{" EnumConstants [";" BodyDeclarations] "}"

UnionBody:
    "{" UnionTypes [";" BodyDeclarations] "}"

DeclarationExtensibility:
    (one of)
    "open" "abstract"

StructLayout:
    (one of)
    "ordered" "packed"

StructBody:
    "{" [BodyDeclarations] "}"

--

BodyDeclarations:
    BodyDeclaration {BodyDeclaration}

EnumConstants:
    EnumConstant {"," EnumConstant}

UnionTypes:
    UnionType {"," UnionType}

--

BodyDeclaration:
    StaticInitializer
    [DeclarationEncapsulation] MemberDeclaration
    [DeclarationEncapsulation] TypeDeclaration

EnumConstant:
    Identifier ["=" VariableInitializer]

UnionType:
    TypedefDeclaration
    EnumDeclaration
    UnionDeclaration
    StructDeclaration

--

StaticInitializer:
    "static" [StringIdentifier] Block

MemberDeclaration:
    [MemberStaticity] FieldDeclaration
    [MemberStaticity] MethodDeclaration

VariableInitializer:
    Expression
    ArrayInitializer
    StructInitializer

--

MemberStaticity:
    "static"

FieldDeclaration:
    ValueMutability [ValueVolatility] Identifier [StringIdentifier] ":" Type ["=" VariableInitializer] ";"

MethodDeclaration:
    [DeclarationExtensibility] [MethodOverride] [FunctionPurity] "func" MethodHeader MethodBody

--

MethodOverride:
    "override"

MethodHeader:
    MethodDeclarator ["->" Result]

MethodBody:
    Block
    ";"

--

MethodDeclarator:
    Identifier [StringIdentifier] [":" TypeName] "(" [Parameters] ")"

--

Parameters:
    ThisParameter ["," FixedParameters] ["," VariableArityParameter]
    FixedParameters ["," VariableArityParameter]
    VariableArityParameter

--

FixedParameters:
    FixedParameter {"," FixedParameter}

VariableArityParameter:
    "..." Identifier [":" Type]

--

FixedParameter:
    Identifier [":" Type]

## Types
Type:
    PrimitiveType [PointerOrArraySuffix]
    TypeName [PointerOrArraySuffix]
    VoidPointerType [PointerOrArraySuffix]
    FunctionType
    "(" FunctionType ")" PointerOrArraySuffix

--

PrimitiveType:
    [TypeAtomicity] NumericType
    [TypeAtomicity] "bool"
    [TypeAtomicity] "_char"

PointerOrArraySuffix:
    PointerSuffix [PointerOrArraySuffix]
    ArrayDim [PointerOrArraySuffix]

TypeName:
    [TypeBareness] Identifier {"." Identifier}

VoidPointerType:
    "unsafe" "void" "&" [TypeAtomicity]

FunctionType:
    [TypeAtomicity] [FunctionPurity] "(" [ParameterTypes] ")" "->" Result

--

TypeAtomicity:
    "atomic"

NumericType:
    IntegralType
    FloatingPointType

PointerSuffix:
    [ValueMutability] [ValueVolatility] "&" [PointerWidth] [ReferenceAliasability]
    [ValueMutability] [ValueVolatility] "&" TypeAtomicity [ReferenceAliasability]

ArrayDim:
    "[" [Expression] "]"
    "[" TypeBareness "]"

TypeBareness:
    "unsafe" "bare"

FunctionPurity:
    ["local"] "const"
    ["local"] "pure"

ParameterTypes:
    ThisParameter ["," FixedParameterTypes] ["," VariableArityParameterType]
    FixedParameterTypes ["," VariableArityParameterType]
    VariableArityParameterType

Result:
    "noreturn"
    "void"
    Type

--

IntegralType:
    (one of)
    "_ubyte" "_byte" "_ushort" "_short" "_uint" "_int" "_ulong" "_long"

FloatingPointType:
    (one of)
    "_float" "_double"

ValueMutability:
    ["unused"] "var"
    ["local"] "const"

ValueVolatility:
    ["local"] "volatile"

PointerWidth:
    [["unsafe"] "unused"] "wide"
    "unsafe" "bare"

ReferenceAliasability:
    ["local"] "aliasable"

ThisParameter:
    "this" [":" TypeName [ValueMutability] [ValueVolatility] ["&" [PointerWidth] [ReferenceAliasability]]]
    "this" ":" ValueMutability [ValueVolatility] ["&" [PointerWidth] [ReferenceAliasability]]
    "this" ":" ValueVolatility ["&" [PointerWidth] [ReferenceAliasability]]
    "this" ":" "&" [PointerWidth] [ReferenceAliasability]

FixedParameterTypes:
    FixedParameterType {"," FixedParameterType}

VariableArityParameterType:
    "..." ":" Type

--

FixedParameterType:
    ":" Type

## Blocks and Statements
Block:
    "{" [BlockStatements] "}"

--

BlockStatements:
    BlockStatement {BlockStatement}

## Expressions

## Array and Struct Initializers
ArrayInitializer:
    "[" [VariableInitializers] "]"

StructInitializer:
    "{" [FieldInitializers] "}"

--

VariableInitializers:
    VariableInitializer {"," VariableInitializer}

FieldInitializers:
    FieldInitializer {"," FieldInitializer}

--

FieldInitializer:
    Identifier "=" VariableInitializer
